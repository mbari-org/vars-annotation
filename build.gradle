plugins {
    id 'com.adarshr.test-logger' apply false
    id 'com.github.spotbugs' apply  false
    id 'org.kordamp.gradle.jdeps' apply false
    id "com.github.ben-manes.versions" apply false
    id "org.javamodularity.moduleplugin" apply false
}


subprojects {

    version = "1.4.0"

    apply plugin: 'com.adarshr.test-logger'
    apply plugin: 'com.github.ben-manes.versions'
    apply plugin: 'com.github.spotbugs'
    apply plugin: 'java'
    apply plugin: 'maven-publish'
    apply plugin: 'org.javamodularity.moduleplugin'
    apply plugin: 'org.kordamp.gradle.jdeps'

    sourceCompatibility = 17
    targetCompatibility = 17

    repositories {
//        jcenter()
        mavenLocal()
        mavenCentral()
        maven {
            name = "MBARI"
            url = uri("https://maven.pkg.github.com/mbari-org/maven")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_USERNAME")
                password = project.findProperty("gpr.key") ?: System.getenv("GITHUB_TOKEN")
            }
        }
    }

    configurations {
        testCompile
    }

    test {
        useJUnitPlatform()
        moduleOptions {
            runOnClasspath = true
        }
        testlogger {
            theme 'mocha' // task level
        }
    }

    ext {
        javafxVersion = "19"
        // javafxVersion = "17.0.2"
//        javafxVersion = "11.0.2"
    }

    dependencies {

        constraints {
            implementation 'com.auth0:java-jwt:3.18.3'
            implementation 'com.fasterxml.jackson.core:jackson-databind:2.14.1'
            implementation 'com.fatboyindustrial.gson-javatime-serialisers:gson-javatime-serialisers:1.1.2'
            implementation 'com.github.ben-manes.caffeine:caffeine:3.1.2'
            implementation 'com.github.mizosoft.methanol:methanol:1.7.0'
            implementation 'com.google.code.gson:gson:2.10'
            implementation 'com.google.inject:guice:5.1.0'
            implementation 'com.jfoenix:jfoenix:9.0.10'
            implementation 'com.squareup.okhttp3:logging-interceptor:3.14.4'
            implementation 'com.squareup.retrofit2:converter-gson:2.9.0'
            implementation 'com.squareup.retrofit2:retrofit:2.9.0'
            implementation 'com.typesafe:config:1.4.2'
            implementation 'de.jensd:fontawesomefx-commons:11.0'
            implementation 'de.jensd:fontawesomefx-materialicons:2.2.0-11'
            implementation 'eu.hansolo:Medusa:16.0.0' //1.7
            implementation 'io.reactivex.rxjava3:rxjava:3.1.5'
            implementation 'jakarta.activation:jakarta.activation-api:1.2.2'
            implementation 'javax.inject:javax.inject:1'
            implementation 'javax.servlet:javax.servlet-api:4.0.1'
            implementation 'org.controlsfx:controlsfx:11.1.2'
            implementation 'org.glassfish.jersey.core:jersey-client:2.29'
            implementation 'org.kordamp.ikonli:ikonli-javafx:12.3.1'
            implementation 'org.kordamp.ikonli:ikonli-material-pack:12.3.1'
            implementation 'org.zeromq:jeromq:0.5.3'
            implementation 'org.mbari:imgfx:0.0.13'
            implementation 'org.mbari.vcr4j:vcr4j-core:5.0.0.jre17-SNAPSHOT'
            implementation 'org.mbari.vcr4j:vcr4j-remote:5.0.0.jre17-SNAPSHOT'
            implementation 'org.mbari.vcr4j:vcr4j-sharktopoda-client:5.0.0.jre17-SNAPSHOT'
            implementation 'org.mbari.vcr4j:vcr4j-sharktopoda:5.0.0.jre17-SNAPSHOT'
            implementation 'org.mbari:mbarix4j:2.0.5.jre11'
            implementation 'org.slf4j:slf4j-api:2.0.6'
            implementation 'org.slf4j:slf4j-jdk14:2.0.6'
            runtimeOnly 'ch.qos.logback:logback-classic:1.4.5'
            runtimeOnly 'org.fusesource.jansi:jansi:2.4.0'
            runtimeOnly 'org.slf4j:slf4j-jdk14:2.0.3'
        }

        testCompileOnly 'junit:junit:4.13.2'
        testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.1'
        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.1'
        testRuntimeOnly 'org.junit.vintage:junit-vintage-engine:5.9.1'


    }

    task tree(type: DependencyReportTask) {}

    spotbugs {
        ignoreFailures = true
    }

    jdepsReport {
        classpaths = ['compileClasspath', 'runtimeClasspath']
    }

    publishing {
        repositories {
           maven {
               name = "GitHubPackages"
               url = uri("https://maven.pkg.github.com/mbari-org/maven")
               credentials {
                   username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_USER") ?: System.getenv("GITHUB_USERNAME")
                   password = project.findProperty("gpr.key") ?: System.getenv("GITHUB_TOKEN")
               }
           }
        }
        publications {
            // maven(MavenPublication) {
            //     groupId "org.mbari.vars"
            //     artifactId project.name
            //     from components.java
            // }
           gpr(MavenPublication) {
               groupId "org.mbari.vars"
               artifactId project.name
               from components.java
           }
        }
    }

}

