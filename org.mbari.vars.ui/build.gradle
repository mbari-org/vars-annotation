plugins {
    id 'application'
    id 'org.openjfx.javafxplugin'
    id 'org.beryx.jlink'
}

dependencies {
    implementation project(':org.mbari.vars.core')
    implementation project(':org.mbari.vars.services')

    implementation 'com.jfoenix:jfoenix'
    implementation 'com.typesafe:config'
//    implementation 'io.reactivex.rxjava2:rxjava'
    implementation 'javax.inject:javax.inject'
    implementation 'org.kordamp.ikonli:ikonli-javafx'
    implementation 'org.kordamp.ikonli:ikonli-material-pack'
//    implementation 'org.mbari.vars:vars-avfoundation'
    implementation 'org.zeromq:jeromq'
    implementation 'org.mbari.vcr4j:vcr4j-core'
//    implementation 'org.mbari.vcr4j:vcr4j-jserialcomm'
    implementation 'org.mbari.vcr4j:vcr4j-sharktopoda'
    implementation 'org.mbari.vcr4j:vcr4j-sharktopoda-client'
    implementation 'org.mbari:mbarix4j'
    implementation 'org.slf4j:slf4j-api'

//    implementation('com.google.inject:guice') {
//        exclude group: 'com.google.code.findbugs', module: 'jsr305'
//        exclude group: 'org.checkerframework', module: 'checker-compat-qual'
//    }

//    implementation('io.reactivex.rxjava2:rxjavafx') {
//        exclude group: 'org.openjfx', module: 'javafx-base'
//        exclude group: 'org.openjfx', module: 'javafx-controls'
//        exclude group: 'org.openjfx', module: 'javafx-graphics'
//        because("If not excluded we end up with both mac and linux javafx jars which cause jlink to fail")
//    }

    implementation('org.controlsfx:controlsfx') {
        exclude group: 'org.openjfx', module: 'javafx-base'
        exclude group: 'org.openjfx', module: 'javafx-controls'
        exclude group: 'org.openjfx', module: 'javafx-graphics'
        because("If not excluded we end up with both mac and linux javafx jars which cause jlink to fail")
    }

//    implementation('org.mbari.m3:vars-blackmagic') {
//        exclude group: 'org.slf4j', module: 'slf4j-simple'
//        because("Do not want this slf4j provider included on classpath")
//    }

    runtimeOnly('ch.qos.logback:logback-classic') {
        exclude group: 'javax.activation'
    }
    runtimeOnly 'javax.servlet:javax.servlet-api'
    runtimeOnly 'org.fusesource.jansi:jansi'

    // For command line comment out runtime and add implementation so we see log output.
//    runtimeOnly 'org.slf4j:slf4j-jdk14'
//    implementation 'org.slf4j:slf4j-jdk14'
//    runtimeOnly 'com.fazecast:jSerialComm'
}

javafx {
    version = javafxVersion
    modules = [
            'javafx.base',
            'javafx.controls',
            'javafx.fxml',
            'javafx.graphics'
    ]
}


/* *****************************************************************************
  These runtime args are shared by several tasks. We add many env params that
  configure values defined in reference.conf. Note that the conf params will
  only be added to the launchers IF they are defined in the system env at
  build time
 */

def runtimeJvmArgs = [
        '-Xms1g',
        '--add-exports', 'javafx.base/com.sun.javafx.binding=com.jfoenix',
        '--add-exports', 'javafx.base/com.sun.javafx.event=com.jfoenix',
        '--add-exports', 'javafx.controls/com.sun.javafx.scene.control.behavior=com.jfoenix',
        '--add-exports', 'javafx.controls/com.sun.javafx.scene.control=com.jfoenix',
        '--add-exports', 'javafx.graphics/com.sun.javafx.stage=com.jfoenix',
        '--add-opens', 'java.base/java.lang.invoke=retrofit2',
        '--add-opens', 'java.base/java.lang.invoke=vars.annotation.merged.module',
        '--add-opens', 'java.base/java.lang.reflect=com.jfoenix',
        '--add-opens', 'org.mbari.vars.services/org.mbari.vars.services.model=com.google.gson',
        '--add-reads', 'vars.annotation.merged.module=org.slf4j'
]

// If any of these exist as environment variables add them as -Dkey=value to jvmArgs
def confEnvParams = [
        "ACCOUNTS_SERVICE_URL",
        "ACCOUNTS_SERVICE_TIMEOUT",
        "ACCOUNTS_SERVICE_CLIENT_SECRET",
        "ANNOTATION_SERVICE_URL",
        "ANNOTATION_SERVICE_TIMEOUT",
        "ANNOTATION_SERVICE_CLIENT_SECRET",
        "ANNOTATION_SERVICE_PAGING",
        "ANNOTATION_SERVICE_PAGE_COUNT",
        "ANNOTATION_SERVICE_V2_URL",
        "APP_IMAGE_COPYRIGHT_OWNER",
        "CONCEPT_SERVICE_URL",
        "CONCEPT_SERVICE_TIMEOUT",
        "CONCEPT_SERVICE_TEMPLATE_FILTERS",
        "MEDIA_SERVICE_URL",
        "MEDIA_SERVICE_TIMEOUT",
        "MEDIA_SERVICE_CLIENT_SECRET",
        "PANOPTES_SERVICE_URL",
        "PANOPTES_SERVICE_TIMEOUT",
        "PANOPTES_SERVICE_CLIENT_SECRET",
        "PREFERENCES_SERVICE_URL",
        "PREFERENCES_SERVICE_TIMEOUT",
        "PREFERENCES_SERVICE_CLIENT_SECRET",
        "SHARKTOPODA_DEFAULTS_CONTROL_PORT",
        "SHARKTOPODA_DEFAULTS_FRAMEGRAB_PORT"
]
confEnvParams.each { k ->
    def v = System.getenv(k)
    if (v) {
        runtimeJvmArgs.add("-D${k}=${v}")
    }
}

tasks.withType(JavaExec) {
    if (System.getProperty('DEBUG', 'false') == 'true') {
        def debugJvmArgs = ['-Xdebug', '-Xrunjdwp:transport=dt_socket,server=y,suspend=y,address=5005']
        debugJvmArgs.addAll(runtimeJvmArgs)
        jvmArgs(debugJvmArgs)
    }
}

/* *****************************************************************************
 Use the jlink plugin to build a dist with a JVM
 */
application {
    mainModule = "org.mbari.vars.ui"
//    mainClassName = "org.mbari.vars.ui.App"
    mainClassName = project.hasProperty("mainClass") ? project.getProperty("mainClass") : "org.mbari.vars.ui.App"
    applicationDefaultJvmArgs = runtimeJvmArgs
}

task execute(type:JavaExec) {
//    mainClass = project.hasProperty("mainClass") ? getProperty("mainClass") : "org.mbari.vars.ui.App"
    classpath = sourceSets.main.runtimeClasspath
}


jlink {
    imageZip = file("$buildDir/image-zip/vars-annotation.zip")
    options = [
            '--bind-services',
            '--strip-debug',
            '--compress', '2',
            '--no-header-files',
            '--no-man-pages'
    ]

    if(System.getenv("CI")) {
        ["win", "linux", "mac"].each { name ->
            targetPlatform(name) {
                jdkHome = System.getenv("JDK_HOME_${name.toUpperCase()}")
                addExtraModulePath(System.getenv("OPENJFX_MODS_${name.toUpperCase()}"))
            }
        }
    }

    launcher {
        name = 'VARS Annotation'
        jvmArgs = runtimeJvmArgs
    }

    jpackage {
        // jpackageHome = System.getenv("JPACKAGE_HOME")
        // jpackageHome = "C:\\Program Files\\AdoptOpenJDK\\jdk-14.0.0.36-hotspot"

        def customInstallerOptions = [
                "--app-version", project.version,
                "--copyright", "Monterey Bay Aquarium Research Institute 2020",
                "--name", "VARS Annotation",
                "--vendor", "org.mbari"
        ]

        if  (org.gradle.internal.os.OperatingSystem.current().isLinux()) {
            installerType = "deb"
        }
        else if (org.gradle.internal.os.OperatingSystem.current().isWindows()) {
            installerType = "msi"
            customInstallerOptions.addAll([
                "--win-upgrade-uuid", "1afa1e7b-8c54-4007-9ccb-537b61826967",
                "--win-menu-group", "VARS",
                "--win-menu"
            ])
        }
        else {
            installerType = "dmg"
            customInstallerOptions.addAll([
                "--mac-package-name", "VARS Annotation",
                "--mac-package-identifier", project.name
            ])
            imageOptions = ["--icon", "src/jpackage/macos/VARS Annotation.icns"]

            // def prefix = System.getenv("MAC_PACKAGE_SIGNING_PREFIX")
            // def user=System.getenv("MAC_SIGNING_KEY_USER_NAME")
            // if (prefix && user) {
            //     customInstallerOptions.addAll([
            //         "--mac-sign",
            //         "--mac-package-signing-prefix", prefix,
            //         "--mac-signing-key-user-name", user
            //     ])
            // }

        }
        installerOptions = customInstallerOptions

    }

}

tasks.jpackageImage.doLast {
    if (org.gradle.internal.os.OperatingSystem.current().isMacOsX()) {
        // copy {
        //     println("MACOSX: Adding VARS Annotation.icns")
        //     from "src/jpackage/macos"
        //     include "VARS Annotation.icns"
        //     into "build/jpackage/VARS Annotation.app/Contents/Resources"
        // }
        exec {
            println("MACOSX: Modifying Info.plist - Adding Camera Entitlement")
            workingDir "build/jpackage/VARS Annotation.app/Contents"

            executable 'plutil'
    
            args '-insert', 'NSCameraUsageDescription', '-string',  
              'VARS Annotation uses camera access for framecapture from hardware', 
              'Info.plist'
        }

        exec {
            println("MACOSX: Modifying Info.plist - Adding Microphone Entitlement")
            workingDir "build/jpackage/VARS Annotation.app/Contents"

            executable 'plutil'

            args '-insert', 'NSMicrophoneUsageDescription', '-string',
                    'VARS Annotation uses microphone access to avoid crashes with Apple GateKeeper',
                    'Info.plist'
        }

        
        def signer = System.getenv("MAC_CODE_SIGNER")
        if (signer) {
            exec {
                println("MACOSX: Signing application")
                workingDir "build/jpackage"

                executable "codesign"

                args "--entitlements", "${projectDir}/src/jpackage/macos/java.entitlements",
                  "--options", "runtime", "--deep", "-vvv", "-f", "--sign", signer, 
                  "VARS Annotation.app"
            }
        }
    }
}